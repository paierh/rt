<%PERL>

use Data::Dumper;
my $stringify = sub {
    my $value = shift;
    return "" if !defined($value);

    local $Data::Dumper::Terse = 1;
    local $Data::Dumper::Indent = 2;
    local $Data::Dumper::Sortkeys = 1;
    my $output = Dumper $value;
    chomp $output;
    return $output;
};

my $doc_version = $RT::VERSION;
$doc_version =~ s/rc\d+//; # 4.4.2rc1 -> 4.4.2
$doc_version =~ s/\.\d+-\d+-g\w+$//;  # 4.4.3-1-g123 -> 4.4

my $name = $option->{Name};
my $meta = RT->Config->Meta( $name );
return if $meta->{Invisible} || $meta->{Deprecated};

my $has_execute_code = $session{CurrentUser}->HasRight(Right => 'ExecuteCode', Object => RT->System);

my $raw_value = RT->Config->Get( $name );
my $val = $stringify->($raw_value);
my $doc_url = "https://docs.bestpractical.com/rt/$doc_version/RT_Config.html#$option->{Help}";
my $widget = $meta->{'Widget'} || '/Widgets/Form/Code';
my $is_code = $widget eq '/Widgets/Form/Code';
my $is_password = ($name =~ /Password/i and $name !~ /MinimumPasswordLength|AllowLoginPasswordAutoComplete/ );
my $is_immutable = $meta->{Immutable}
                || $meta->{Obfuscate}
                || ($is_code && $val =~ s/sub \{ "DUMMY" \}/sub { ... }/g)
                || ($is_code && !$has_execute_code);

my $current_value = $is_code ? $val : $raw_value;
my $args   = $meta->{'WidgetArguments'} || {};

if ($widget eq '/Widgets/Form/Boolean') {
    %$args = (
        Default => 0,
        RadioStyle => 1,
        %$args,
    );
}
elsif ($widget eq '/Widgets/Form/String' || $widget eq '/Widgets/Form/Integer') {
    %$args = (
        Size => 60,
        %$args,
    );
}
my $row_start = qq{<div class="widget form-row">
  <span class="col-md-3 label"><a href="$doc_url" target="_blank">$name</a></span>
  <span class="col-md-9 value">
};
my $row_end = qq{</span></div>};

</%PERL>

<!-- start option <% $name %> -->
% if ( $meta->{EditLink} ) {
% if ($widget eq '/Widgets/Form/MultilineString' || $widget eq '/Widgets/Form/Code') {
<% $row_start |n %><textarea disabled class="<% $is_code ? 'code' : '' %>" rows="6" cols="80"><% $current_value %></textarea><br />
% } else {
<% $row_start |n %><input type="text" disabled width="80" value="<% $current_value %>" /><br/>
% }
<&|/l_unsafe, "<a href=\"$meta->{EditLink}\">", loc($meta->{EditLinkLabel}), "</a>" &>Visit [_1][_2][_3] to manage this setting</&>
% } elsif ( $name =~ /Plugins/) {
<% $row_start |n %><ul class="plugins">
% for my $plugin (RT->Config->Get($name)) {
<li><a href="https://metacpan.org/search?q=<% $plugin |u %>" target="_blank"><% $plugin %></a></li>
% }
</ul><% $row_end |n%>
<br /><em><% loc('Must modify in config file' ) %></em>
% } elsif ( $is_password ) {
<em><% loc('Must modify in config file' ) %></em><br />
% } elsif ( $is_immutable ) {
% if ($widget eq '/Widgets/Form/MultilineString' || $widget eq '/Widgets/Form/Code') {
<% $row_start |n %><textarea disabled class="<% $is_code ? 'code' : '' %>" rows="6" cols="80"><% $current_value %></textarea>
% } else {
<% $row_start |n %><input type="text" disabled width="80" value="<% $current_value %>" />
% }
<br /><em><% loc('Must modify in config file' ) %></em>
<% $row_end |n %>
% } else { 
  <& $widget,
    Default      => 1,
    DefaultValue => '',
    DefaultLabel => '(no value)',

    %{ $m->comp('/Widgets/FinalizeWidgetArguments', WidgetArguments => $args ) },
    Name         => $name,
    LabelLink    => $doc_url,
    CurrentValue => $current_value,
    Description  => $name,
    Hints        => $meta->{WidgetArguments}->{Hints} || '',
  &>
<textarea class="hidden" name="<% $name %>-Current"><% $current_value %></textarea>
% }
<!-- end option <% $name %> -->
<%ARGS>
$option
</%ARGS> 
